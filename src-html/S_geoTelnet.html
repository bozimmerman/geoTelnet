<!DOCTYPE html><html><head><title>Untitled</title></head><body><pre>
.if Pass1
.noeqin
.noglbl
.include GEOSequates
.eqin
.glbl
.endif

curWidth = $8807

.psect
StartPrg:	jsr	scnSetup
	ldb	flgEcho,#1
	jsr	FixEcho
	ldb	flgANSI,#1
	jsr	FixATerm
	jsr	mdmSetup	; *
	jsr	FixBaud
	jsr	keySetup
	rts

CleanExit:	jsr	PromptOff
	jsr	keyRestore
	jsr	mdmDone
	jsr	SavColorScn	; restores origColor
	jmp	EnterDeskTop

scnSetup:	ldb	txtTop,#$10	; screen def
	ldb	txtBot,#199
	ldw	txtLeft,#$00
	ldw	txtRight,#319
	mvw	txtRight,txtREnd	; last chars
	sbw	curWidth,txtREnd
	inw	txtREnd	
	mvb	txtBot,txtBEnd	; bot row
	inc	txtBEnd
	sbb	curHeight,txtBEnd
	mvb	COLOR_MATRIX,origColor
	lda	#0
	jsr	ClrTrmScn
	jsr	UseSystemFont	; * for now
	ldw	A2,#0	; "About" refresh
	ldw	A3,#DAdone	; DA refresh
	ldw	A4,#DAinit	; DA init
	ldw	A5,#AboutStrs
	jsr	InitDaAb
	ldb	dispBufferOn,#ST_WR_FORE
	jsr	DrawMenu
scnReset:	jsr	FixFont80
	mvb	baseColor,curColor
	jsr	CrsrTL	
	mvb	txtXY+2,R1H
	mvw	txtXY,R11
	ldb	windowTop,#0	; necc for menu recover?
	mvb	txtBot,windowBottom,
	mvw	txtLeft,leftMargin
	mvw	txtRight,rightMargin
	rts



ClrTrmScn:	pha
	lda	#0
	sta	ansiDex
	sta	ansiNBuf
	jsr	SetPattern
	pla
	sta	R2L
	mvb	txtBot,R2H
	mvw	txtLeft,R3
	mvw	txtRight,R4
	ldb	dispBufferOn,#ST_WR_FORE|ST_WR_BACK
	jsr	Rectangle
	ldb	dispBufferOn,#ST_WR_FORE
	ldb	baseColor,#(7*16)	; yellow on black
	ldw	R1,#COLOR_MATRIX+(40*2)
	ldw	R0,#$3e8-(40*2)	; set initial scrn color
	mvb	baseColor,R2L
	jsr	FillRam	; color the term screen
	rts

FixFont80:	ldw	R0,#GEOSFont
	jsr	LoadCharSet
	ldb	baselineOffset,#0
	ldb	curWidth,#$04	; curWidth=realSize
	rts

AboutStrs:	.byte	24,"      geoTelnet beta1",0
	.byte	24,"  For C64Net WiFi Modem",0
	.byte	24,"    By Bo Zimmerman",0
	.byte	24,"      bo@zimmers.net",0

mdmSetup:	ldb	A2L,#>CLBuf	; input buffer
	ldb	A2H,#CLBfLen	; input buffer len pages
	ldb	A3L,#$01	; 1200 baud
	ldb	A3H,#$80	; RTSCTS Flow Control!
	jsr	ComInit
	ldw	R0,#mdmInitStr
	jsr	ComPutS
	rts

mdmDone:	jsr	ComDisable
	jsr	ComUninst
	rts

mdmInitStr:	.byte 13,"  atze0v1x1f0q0r0&p0i0",13,0
mdmDoneStr:	.byte 13,"  atzb1200",13,0


ComPutS:	ldy	#0	; *
10$:	lda	(R0),y
	beq	20$
	jsr	ComSPut
	iny
	bne	10$
20$:	rts



keySetup:	mvw	keyVector,savKeyVec
	sei
	ldw	keyVector,#KeyPRtn
	ldw	intBotVector,#MainLp
	cli
	ldy	curHeight
	dey
	tya	
	jsr	InitTextPrompt
	mvw	txtXY,stringX
	mvb	txtXY+2,stringY
	jsr	PromptOn
	rts

keyRestore:	sei
	ldw	intBotVector,#0
	mvw savKeyVec,keyVector
	cli
	jsr	PromptOff
	ldb	$84b4,#0	; why didn't promptoff do this?
	rts

KeyPRtn:	lda	keyData
	beq	99$
	;TODO: *** turn arrow keypress in ansi esc, if ansi on
	cmp	#$1d
	bne	10$
	lda	#$08
10$:	ldx	flgEcho
	beq	30$
	pha
	jsr	SafePutC
	pla
30$:	pha
	jsr	ComSPut	; *
	; send to modem
	pla
99$:	rts



PutChars:	mvb	txtXY+2,R1H
	mvw	txtXY,R11
PutChar2:	mvw	R0,PutCharLp+3
PutCharLp:	ldy	#0
	lda	$9999
	beq	30$
20$:	jsr	SPutChar
	inw	PutCharLp+3
	bra	PutCharLp	
30$:	mvw R11,txtXY
	mvb	R1H,txtXY+2
	rts

SafePutC:	pha
	mvb	txtXY+2,R1H
	mvw	txtXY,R11
	pla
	jsr	SPutChar
	mvw R11,txtXY
	mvb	R1H,txtXY+2
	rts

SPutChar:	cmp	#27
	bne	10$
	jmp	AnsiStart
10$:	pha
	lda	ansiDex
	beq	11$
	pla
	jmp	AnsiCont
11$:	pla
	cmp	#$0d
	bne	13$
	jmp	PutLine
13$:	cmp	#$08
	bne	20$
	jmp	PutDel
20$:	cmp	#$20
	bcs	30$
29$:	rts
30$:	jsr	PutChar
	lda	curColor
	.byte	$8d	; sta
colorPtr:	.word COLOR_MATRIX
	dec	colorHalf
	beq	10$
	ldb	colorHalf,#1
	inw	colorPtr
10$:	ldb	flgDoBg,#1
	cpw	R11,txtRight
	bcs	PutLine
	rts


PutLine:	ldw	R11,#0
	ldb	colorHalf,#1
	adb	curHeight,R1H
	cpb	R1H,txtBot
	bcs	10$
	cwi	colorLPtr,#$8fc0
	bcs	05$
	avw	#40,colorLPtr
05$:	mvw	colorLPtr,colorPtr
	jsr	ClrColLn
	rts
10$:	sbb	curHeight,R1H
	mvw	colorLPtr,colorPtr
	ldw	R0,#SCREEN_BASE+(24*40)
	ldw	R1,#SCREEN_BASE+(16*40)
	ldw	R2,#$1f3f-(24*40)
	jsr	MoveData
	ldw	R0,#COLOR_MATRIX+(3*40)
	ldw	R1,#COLOR_MATRIX+(2*40)
	ldw	R2,#$03e7-(3*40)
	jsr	MoveData
	ldw	R1,#SCREEN_BASE+$1f3f-(8*40)
	ldw	R0,#8*40
	ldb	R2L,#0
	jsr	FillRam
	ldw	R1,#BACK_SCR_BASE+$1f3f-(8*40)
	ldw	R0,#8*40
	ldb	R2L,#0
	jsr	FillRam
	ldb	flgDoBg,#1
	mvb	txtBot,R1H
	sbb	curHeight,R1H
	inc	R1H
	jsr	ClrColLn
	rts

ClrColLn:	tya
	pha
	mvw	colorLPtr,R0
	ldy	#0
	lda	baseColor
10$:	sta	(R0),y
	iny
	cpy	#40
	bcc	10$
20$:	pla
	tay
	rts

PutDel:	cwi	R11,#3	; todo: txtLeft
	bcs	15$
	mvw	txtRight,R11
	ldb	colorHalf,#0
	cbi	R1H,#$20	; todo: txtHeight?
	bcc	15$
	sbb	curHeight,R1H
	svw	#40,colorLPtr
	mvw	colorLPtr,colorPtr
	avw	#40,colorPtr
15$:	dec	colorHalf
	beq	20$
	ldb	colorHalf,#1
	dew	colorPtr
20$:	sbw	curWidth,R11	; realSize, #$04
	lda	#$20
	jsr	PutChar
	sbw	curWidth,R11
	ldb	flgDoBg,#1
	rts


CrsrUp:	cpb	txtTop,txtXY+2	; Cursor Up
	bcc	10$
	rts
10$:	svw	#40,colorLPtr
	svw	#40,colorPtr
	sbb	curHeight,txtXY+2
	rts

CrsrDn:	cpb	txtXY+2,txtBEnd	; Cursor Down
	bcc	10$
	rts
10$:	avw	#40,colorLPtr
	avw	#40,colorPtr
	adb	curHeight,txtXY+2
	rts

CrsrRt:	cpw	txtXY,txtREnd	; Cursor Forward
	bcc	10$
	rts
10$:	dec	colorHalf
	beq	20$
	inw	colorPtr
	ldb	colorHalf,#1
20$:	adw	curWidth,txtXY
	rts

CrsrLt:	cpw	txtLeft,txtXY	; Cursor Backward
	bcc	10$
	rts
10$:	dec	colorHalf
	beq	20$
	dew	colorPtr
	ldb	colorHalf,#1
20$:	sbw	curWidth,txtXY
	rts

CrsrTL:	mvb	txtTop,txtXY+2
	ldw	colorLPtr,#COLOR_MATRIX+(40*2)
	jsr	CrsrStLn
	rts

CrsrStLn:	mvw	txtLeft,txtXY
	mvw	colorLPtr,colorPtr
	ldb	colorHalf,#1
	rts


MainLp:	mvb	rrate,A5L
	mvb	txtXY+2,R1H
	mvw	txtXY,R11
MainLp2:	jsr	ComGet
	bcc	20$
	lda	flgDoBg
	beq	10$
	mvw	R11,stringX
	mvb	R1H,stringY
	jsr	PromptOn
10$:	mvb	R1H,txtXY+2
	mvw	R11,txtXY
	rts
20$:	tax
	lda	$01
	pha
	lda	#$30
	sta	$01
	txa
	jsr	SPutChar
	pla
	sta	$01
	dec	A5L
	beq	30$
	jmp	MainLp2
30$:	mvb	R1H,txtXY+2
	mvw	R11,txtXY
	rts


CLBfLen 	= 4		; buffer because not 0 aligned
CLBuf	= $5c00
txtTop:	.byte	$10
txtBot:	.byte	199
txtLeft:	.word	0
txtRight:	.word	319
txtBEnd:	.byte	199
txtREnd:	.word	312

.ramsect
rrate:	.block	1
savKeyVec:	.block	2
txtXY:	.block	3
flgDoBg:	.block	1
flgEcho:	.block	1
flgANSI:	.block	1
origColor:	.block	1
baseColor:	.block	1
curColor:	.block	1
colorLPtr:	.block	2
colorHalf:	.block	1
colorBBuf:	.block	$03e9-(40*2)
; that's all folks!

</pre></body></html>