<!DOCTYPE html><html><head><title>Untitled</title></head><body><pre>
.if Pass1
.noeqin
.noglbl
.include GEOSequates
.eqin
.glbl
.endif

curWidth = $8807

.psect
StartTelnet:	jsr	scnSetup
	ldb	flgEcho,#1
	jsr	FixEcho
	ldb	flgANSI,#1
	jsr	FixATerm
	jsr	mdmSetup	; *
	jsr	FixBaud
	jsr	keySetup
	rts

CleanExit:	jsr	PromptOff
	jsr	keyRestore
	jsr	mdmDone
	jsr	SavColorScn	; restores origColor
	jmp	EnterDeskTop

scnSetup:	ldb	txtTop,#$10	; screen def
	ldb	txtBot,#199
	ldw	txtLeft,#$00
	ldw	txtRight,#319
	mvw	txtRight,txtREnd	; last chars
	ldb	curWidth+1,#0
	sbw	curWidth,txtREnd
	inw	txtREnd	
	mvb	txtBot,txtBEnd	; bot row
	inc	txtBEnd
	sbb	curHeight,txtBEnd
	mvb	COLOR_MATRIX,origColor
	lda	#0
	jsr	ClrTrmScn
	jsr	UseSystemFont	; * for now
	ldw	A2,#0	; "About" refresh
	ldw	A3,#DAdone	; DA refresh
	ldw	A4,#DAinit	; DA init
	ldw	A5,#AboutStrs
	jsr	InitDaAb
	ldb	dispBufferOn,#ST_WR_FORE
	jsr	DrawMenu
scnReset:	jsr	FixFont80
	mvb	baseColor,curColor
	jsr	CrsrTL	
	mvb	txtXY+2,R1H
	mvw	txtXY,R11
	ldb	windowTop,#0	; necc for menu recover?
	mvb	txtBot,windowBottom,
	mvw	txtLeft,leftMargin
	mvw	txtRight,rightMargin
	rts



ClrTrmScn:	pha
	lda	#0
	sta	ansiDex
	sta	ansiNBuf
	jsr	SetPattern
	pla
	sta	R2L
	mvb	txtBot,R2H
	mvw	txtLeft,R3
	mvw	txtRight,R4
	ldb	dispBufferOn,#ST_WR_FORE|ST_WR_BACK
	jsr	Rectangle
	ldb	dispBufferOn,#ST_WR_FORE
	ldb	baseColor,#(7*16)	; yellow on black
	ldw	R1,#COLOR_MATRIX+(40*2)
	ldw	R0,#$3e8-(40*2)	; set initial scrn color
	mvb	baseColor,R2L
	jsr	FillRam	; color the term screen
	rts

FixFont80:	ldw	R0,#GEOSFont
	jsr	LoadCharSet
	ldb	baselineOffset,#0
	ldb	curWidth,#$04	; curWidth=realSize
	rts

AboutStrs:	.byte	24,"      geoTelnet beta1",0
	.byte	24,"  For C64Net WiFi Modem",0
	.byte	24,"    By Bo Zimmerman",0
	.byte	24,"      bo@zimmers.net",0

mdmSetup:	ldb	A2L,#$00;>CLBuf	; input buffer
	ldb	A2H,#$01;CLBfLen	; input buffer len pages
	ldb	A3L,#$01	; 1200 baud
	ldb	A3H,#$80	; RTSCTS Flow Control!
	ldb	OutBuf,#0
	jsr	ComInit
	jsr	XMOPause
	ldw	R0,#mdmInitStr
	jsr	ComPutS
	rts

mdmDone:	jsr	ComDisable
	jsr	ComUninst
	rts

mdmInitStr:	.byte 13,"  atze0v1x1f0q0r1&p0i0",13,0
mdmDoneStr:	.byte 13,"  atzb1200",13,0


keySetup:	mvw	keyVector,savKeyVec
	sei
	ldw	keyVector,#KeyPRtn
	ldw	intBotVector,#MainLp
	cli
	ldy	curHeight
	dey
	tya	
	jsr	InitTextPrompt
	mvw	txtXY,stringX
	mvb	txtXY+2,stringY
	jsr	PromptOn
	rts

keyRestore:	sei
	ldw	intBotVector,#0
	mvw savKeyVec,keyVector
	cli
	jsr	PromptOff
	ldb	$84b4,#0	; why didn't promptoff do this?
	rts

KeyPRtn:	lda	keyData
	bne	10$
	rts
10$:	ldx	flgEcho
	beq	50$
	cmp	#$0d
	bne	15$
	jsr	SafeDraw
	lda	#$0a
15$:	cmp	#$08
	beq	50$
	cmp	#$1d
	bne	20$
	lda	#$08
20$:	jsr	SafeDraw
50$:	lda	keyData
	jsr	KeyComPut
99$:	rts

KeyComPut:	ldx	flgANSI
	beq	30$
	ldx	#0
11$:	cmp	KeyACt,x
	beq	20$
	inx
	inx
	cpx	#$08
	bcc	11$
	bcs	30$
20$:	inx
	lda	KeyACt,x
	pha
	lda	#27
	jsr	ComSPut
	lda	#91
	jsr	ComSPut
	pla
30$:	cmp	#$1d
	bne	31$
	lda	#$08
31$:	cmp	#$16
	bne	40$
39$:	lda	#$1b
40$:	cmp	#$14
	beq	39$
	pha
	jsr	ComSPut
	pla
	rts
KeyACt:	.byte 16,65,17,66,8,68,30,67,0,0

DrawStr:	mvb	txtXY+2,R1H
	mvw	txtXY,R11
DrwStr2:	mvw	R0,DrwStrLp+3
DrwStrLp:	ldy	#0
	lda	$9999
	beq	30$
20$:	jsr	DrawChar
	inw	DrwStrLp+3
	bra	DrwStrLp	
30$:	mvw R11,txtXY
	mvb	R1H,txtXY+2
	rts

SafeDraw:	pha
	mvb	txtXY+2,R1H
	mvw	txtXY,R11
	pla
	jsr	DrawChar
	mvw R11,txtXY
	mvb	R1H,txtXY+2
	rts



DrawChar:	pha
	lda	ansiDex
	beq	11$
	pla
	jmp	AnsiCont
11$:	lda	telnState
	beq	13$
	pla
12$:	jmp	TelnCont
13$:	pla
	cmp	#27
	bne	14$
	jmp	AnsiStart
14$:	cmp	#$ff
	beq	12$
	cmp	#$0d
	bne	15$
	jmp	DrawCR
15$:	cmp	#$0a
	bne	17$
	jmp	DrawLF
17$:	cmp	#$08
	bne	20$
	jmp	PutDel
20$:	cmp	#$20
	bcs	30$
29$:	rts
30$:	cmp	#$7f
	bcs	29$
	jsr	PutChar	; small put char?
	lda	curColor
	.byte	$8d	; sta
colorPtr:	.word COLOR_MATRIX
	lda	R11L
	and	#$04
	bne	10$
	inw	colorPtr
10$:	inc	flgDoBg
	cpw	R11,txtRight
	bcs	DrwCRLF
	rts


DrwCRLF:	jsr	DrawCR
	jmp	DrawLF

DrawCR:	mvw	txtLeft,R11
	mvw	colorLPtr,colorPtr
	rts

DrawLF:	cpb	R1H,txtBEnd
	bcs	10$
	adb	curHeight,R1H
	cwi	colorLPtr,#$8fc0
	bcs	05$
	avw	#40,colorLPtr
	avw	#40,colorPtr
05$:	rts
10$:	ldw	R0,#SCREEN_BASE+(24*40)
	ldw	R1,#SCREEN_BASE+(16*40)
	ldw	R2,#$1f3f-(24*40)
	jsr	MoveData
	ldw	R0,#COLOR_MATRIX+(3*40)
	ldw	R1,#COLOR_MATRIX+(2*40)
	ldw	R2,#$03e7-(3*40)
	jsr	MoveData
	ldw	R1,#SCREEN_BASE+$1f3f-(8*40)
	ldw	R0,#8*40
	ldb	R2L,#0
	jsr	FillRam
	ldw	R1,#BACK_SCR_BASE+$1f3f-(8*40)
	ldw	R0,#8*40
	ldb	R2L,#0
	jsr	FillRam
	ldb	flgDoBg,#1
	ldw	colorLPtr,#$8fc0
	mvb	txtBot,R1H
	sbb	curHeight,R1H
	inc	R1H
	jsr	ClrColLn
	rts

ClrColLn:	tya
	pha
	mvw	colorLPtr,R0
	ldy	#0
	lda	baseColor
10$:	sta	(R0),y
	iny
	cpy	#40
	bcc	10$
20$:	pla
	tay
	rts

PutDel:	cwi	R11,#3	; todo: txtLeft
	bcs	15$
	mvw	txtRight,R11
	cbi	R1H,#$20	; todo: txtHeight?
	bcc	15$
	sbb	curHeight,R1H
	svw	#40,colorLPtr
	mvw	colorLPtr,colorPtr
	avw	#40,colorPtr
15$:	ldb	curWidth+1,#0
	sbw	curWidth,R11	; realSize, #$04
	lda	#$20
	jsr	SmallPutChar
	ldb	curWidth+1,#0
	sbw	curWidth,R11
	lda	R11L
	and	#$04
	beq	20$
	dew	colorPtr
20$:	ldb	flgDoBg,#1
	rts


CrsrUp:	cpb	txtTop,txtXY+2	; Cursor Up
	bcc	10$
	rts
10$:	svw	#40,colorLPtr
	svw	#40,colorPtr
	sbb	curHeight,txtXY+2
	rts

CrsrDn:	cpb	txtXY+2,txtBEnd	; Cursor Down
	bcc	10$
09$:	rts
10$:	adb	curHeight,txtXY+2
	cwi	colorLPtr,#$8fc0
	bcs	09$
	avw	#40,colorLPtr
	avw	#40,colorPtr
	rts

CrsrRt:	cpw	txtXY,txtREnd	; Cursor Forward
	bcc	10$
	rts
10$:	ldb	curWidth+1,#0
	adw	curWidth,txtXY
	lda	txtXY
	and	#$04
	bne	20$
	inw	colorPtr
20$:	rts

CrsrLt:	cpw	txtLeft,txtXY	; Cursor Backward
	bcc	10$
	rts
10$:	ldb	curWidth+1,#0
	sbw	curWidth,txtXY
	lda	txtXY
	and	#$04
	beq	20$
	dew	colorPtr
20$:	rts

CrsrTL:	mvb	txtTop,txtXY+2
	ldw	colorLPtr,#COLOR_MATRIX+(40*2)
	jsr	CrsrStLn
	rts

CrsrStLn:	mvw	txtLeft,txtXY
	mvw	colorLPtr,colorPtr
	rts


TelnCont:	sty	TelnOut+1
	sta	TelnCC+1
	lda	telnState
	bne	TelnChPt
	ldw	telnStPt,#Tel1Ch
TelnOut:	ldy	#0
	rts
TelnChPt:	mvw	telnStPt,A2
TelnLoop:	ldy	#$00
	lda	(A2),y
	bne	10$
	sta	telnState	; no match, so done
	jmp	TelnOut
10$:	cmp	#$80	; $80 is an Always action
	beq	TelnAct
TelnCC:	cmp	#$ff
	beq	TelnAct
	avw	#3,A2
	jmp	TelnLoop
TelnAct:	iny
	lda	(A2),y
	sta	telnStPt
	sta	TelNDo+1
	iny
	lda	(A2),y
	sta	telnStPt+1
	and	#$80
	bne	10$
	jmp	TelnOut
10$:	lda	(A2),y
	and	#$7f
	sta	TelNDo+2
	ldb	telnState,#0
TelnDo:	jmp	TelnDo



	; initial state machine
Tel1Ch:	.byte 251, <Tel2Ch, >Tel2Ch; WILL
	.byte 252, <Tel3Ch, >Tel3Ch; WONT
	.byte 253, <Tel4Ch, >Tel4Ch; DO
	.byte 254, <Tel5Ch, >Tel5Ch; DONT
	.byte 250, <Tel6Ch, >Tel6Ch; SB
	.byte	0,0,0
	; WILL
Tel2Ch:	.byte	24, <TelWi_TT, >TelWi_TT + $80; TERMTYPE
	.byte	1, <TelWi_Echo, >TelWi_Echo + $80; ECHO
	.byte	3, <TelWi_SGA, >TelWi_SGA + $80; Supp go ah
	.byte	32, <TelWi_SO, >TelWi_SO + $80; Speed opt
	.byte	$80, <TelWi_, >TelWi_	 + $80; everything else
	.byte	0,0,0
	; WONT
Tel3Ch:	.byte	1, <TelWo_Echo, >TelWo_Echo + $80; echo
	.byte	0,0,0	; nothing else to do
	; DO
Tel4Ch:	.byte	24, <TelDo_TT, >TelDo_TT + $80; TERMTYPE
	.byte	1, <TelDo_Echo, >TelDo_Echo + $80; ECHO
	.byte	31, <TelDo_NAWS, >TelDo_NAWS + $80; NAWS
	.byte	3, <TelDo_SGA, >TelDo_SGA + $80; Supp go ah
	.byte	32, <TelDo_SO, >TelDo_SO + $80; Speed opt
	.byte	$80, <TelDo_, >TelDo_	 + $80; everything else
	.byte	0,0,0
	; DONT
Tel5Ch:	.byte	1, <TelDn_Echo, >TelDn_Echo + $80; echo
	.byte	0,0,0	; nothing else to do
	; SB
Tel6Ch:	.byte	$80, <TelSB_, >TelSB_ + $80; save sub-type
	.byte 255, <Tel7Ch, >Tel7Ch	; received end maybe?
	.byte 0,0,0
TelSBCh:	.byte 255, <Tel7Ch, >Tel7Ch	; maybe the end
	.byte	$80, <TelSBCh, >TelSBCh; keep reading
	.byte	0,0,0
Tel7Ch:	.byte	240, <TelSB_Do, >TelSB_Do + $80; received end of SB!
	.byte	$80, <TelSBCh, >TelSBCh; anything else, keep going
	.byte	0,0,0


TelSB_:	lda	TelnCC+1
	sta	TelSB_Do+1
	ldw	telnStPt,#TelSBCh
	jmp	TelnOut

TelSB_Do:	lda	#$00
	cmp	#24
	bne	10$
	ldy	#0
05$:	lda	TelSBttStr,y
	jsr	QOutC
	iny
	cpy	#10
	bcc	05$
	jmp	TelnOut
10$:	cmp	#32
	bne	20$
	ldy	#0
15$:	lda	TelSBsoStr,y
	jsr	QOutC
	iny
	cpy	#15
	bcc	15$
	jmp	TelnOut
20$:	jmp	TelnOut
TelSBttStr:	.byte	255,250,24,0,'a','n','s','i',255,240,0
TelSBsoStr:	.byte	255,250,32,0,'1','2','0','0',44,'1','2','0','0',255,240,0



TelDo_NAWS:	ldy	#0
10$:	lda	TelWiNaStr,y
	jsr	QOutC
	iny
	cpy	#12
	bcc	10$
	jmp	TelnOut
TelWiNaStr:	.byte	255,251,31,255,250,31,0,80,0,23,255,240,0

TelWi_SO:
TelWi_SGA:
TelWi_TT:	mvb TelnCC+1,TelDoAAStr+2
	ldw	R0,#TelDoAAStr
	jsr	QOutStr
	jmp	TelnOut
TelDoAAStr:	.byte	255,253,24,0

TelWi_:	lda	TelnCC+1
	sta	TelDotStr+2
	ldw	R0,#TelDotStr
	jsr	QOutStr
	jmp	TelnOut
TelDotStr:	.byte	255,254,0,0

TelWo_Echo:	ldb	TelWDEcStr+1,254
	jmp	TelWD_Echo
TelDn_Echo:	ldb	TelWDEcStr+1,252
TelWD_Echo:	ldb	flgEcho,#1
	jsr	FixEcho
	ldw	R0,#TelWDEcStr
	jsr	QOutStr
	jmp	TelnOut
TelWDEcStr:	.byte	255,253,1,0

TelDo_SGA:
TelDo_SO:
TelDo_TT:	mvb TelnCC+1,TelWiAAStr+2
	ldw	R0,#TelWiAAStr
	jsr	QOutStr
	jmp	TelnOut
TelWiAAStr:	.byte	255,251,24,0

TelDo_:	mvb	TelnCC+1, TelWotStr+2
	ldw	R0,#TelWotStr
	jsr	QOutStr
	jmp	TelnOut
TelWotStr:	.byte	255,252,0,0

TelWi_Echo:	ldb	TelWDEcStr+1,#253
	jmp	TelW2_Echo
TelDo_Echo:	ldb	TelWDEcStr+1,#251
TelW2_Echo:	ldb	flgEcho,#0
	jsr	FixEcho
	ldw	R0,#TelWDEcStr
	jsr	QOutStr
	jmp	TelnOut


ComPutS:	ldy	#0	; *
10$:	lda	(R0),y
	beq	20$
	jsr	ComSPut
	iny
	bne	10$
20$:	rts

QOutC:	sty	QOutC2+1
	inc	OutBuf
	ldy	OutBuf
	sta	OutBuf,y
	jsr	ComFloff
QOutC2:	ldy	#0
	rts

QOutStr:	sty	QOutStr2+1
	ldy	#0	
QOutStr1:	lda	(R0),y
	beq	QOutStr2
	jsr	QOutC
	iny
	bne	QOutStr1
QOutStr2:	ldy	#0	
	rts

QFlush:	lda	OutBuf
	bne	10$
	rts
10$:	ldy	#1
20$:	lda	OutBuf,y
	jsr	ComSPut
	iny
	cpy	OutBuf
	bcc	20$
	beq	20$
	ldb	OutBuf,#0
	jsr	ComFlon
	rts
	


MainLp:	mvb	rrate,A5L
	mvb	txtXY+2,R1H
	mvw	txtXY,R11
MainLp2:	jsr	ComGet
	bcc	20$
	lda	flgDoBg
	beq	10$
	mvw	R11,stringX
	mvb	R1H,stringY
	jsr	PromptOn
10$:	lda	A9L
	bne	11$
	inc	A9L
	lda	$dc08
	sta	A9H
11$:	cpb	$dc08,A9H
	beq	19$
	mvb	$dc08,A9H
	inc	A9L
	cbi	A9L,#$02
	bcc	19$
	ldb	A9L,#0
	jsr	QFlush
19$:	mvb	R1H,txtXY+2
	mvw	R11,txtXY
	rts
20$:	tax
	ldb	A9L,#0
	lda	$01
	pha
	lda	#$30
	sta	$01
	txa
	jsr	DrawChar
	pla
	sta	$01
	dec	A5L
	beq	30$
	jmp	MainLp2
30$:	mvb	R1H,txtXY+2
	mvw	R11,txtXY
	rts


txtTop:	.byte	$10
txtBot:	.byte	199
txtLeft:	.word	0
txtRight:	.word	319
txtBEnd:	.byte	199
txtREnd:	.word	312
telnStPt:	.byte	0	; combines with state as ptr
telnState:	.byte	0	; combines with above as ptr

.ramsect
rrate:	.block	1
savKeyVec:	.block	2
txtXY:	.block	3
flgDoBg:	.block	1
flgEcho:	.block	1
flgANSI:	.block	1
origColor:	.block	1
baseColor:	.block	1
curColor:	.block	1
colorLPtr:	.block	2
colorBBuf:	.block	$03e9-(40*2)
outBuf:	.block	256
; that's all folks!

</pre></body></html>