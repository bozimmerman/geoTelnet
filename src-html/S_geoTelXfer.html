<!DOCTYPE html><html><head><title>Untitled</title></head><body><pre>
.noeqin
.include	GEOSequates
.glbl
.eqin


.psect



DoXUpload:	jsr	GotoFirstMenu
	jsr	SavColorScn
	jsr	ScreenSafe
	jsr	keyRestore
05$:	ldb	FBOXnod1,#0
	ldb	FBOXnoo2,#$06	; reset disk icon
	cpb	curDrive,VPRGdriv
	bne	10$
	ldb	FBOXnoo2,#0
10$:	ldw	$885b,#$ff00	; whats this?
11$:	jsr	FBOXmvnm
	ldw	R10,#0	; perm filename
	ldb	R7L,#NOT_GEOS	; type of file
	ldw	R0,#FBOXobox
	jsr	DoDlgBox
	lda	sysDBData
	beq	11$
	cmp	#$02	; cancelled?
	bne	20$
15$:	jmp	XferCncl
20$:	cmp	#$06	; change disks?
	bne	23$
	ldw	$885b,#$ff00	; re-clear selection
	jsr	FBOXchgd
	bne	15$
	beq	11$
23$:	cmp	#$05	; something else?
	bne	05$
	ldw	A5,#XFilename	;
	jsr	FBOXopen	; moves filename into XFilename
	lda	XFilename
	beq	15$
	jsr	ClrTrmScn	; clears the screen
	jmp	XUpload

XferCncl:	lda	VPRGdriv
	jsr	VPRGdev
	jsr	VPRGok
	jsr	keySetup
	jmp	ResColorScn	; <-- only if xfer not done


FBOXobox:	.byte	$81
	.byte	16	; getfiles
	.byte	$06,$03
	.byte	11
	.byte	$84,$09
	.word FBOXtxt4
	.byte	12
	.byte	$84,$13
	.byte A6
	.byte	17
	.word FBOXsele
	.byte	$05,$11,$16	; open icon
	.byte	$02,$11,$4c	; cancel icon
FBOXnoo1:	.byte	18	; drive icon
	.byte	$11,$28
	.word FBOXdcon
FBOXnoo2:	.byte $06,$11,$3d
	.byte 0	; end of def
FBOXdcon:	.word FBOXdmag
	.word 0
	.byte	$06, $10
	.word FBOXswdv
FBOXdmag:
<img src="00064.png" />

FBOXsele:	;This routine handles clicks in the filename box
	lda	mouseData	;was button pressed?
	bmi	94$
	jsr	FBOXckch	;check all filenames for click
	cmp	#$ff
	beq	94$	;no filename was selected
	clc
	adc	$885b
	sta	APITemp2	;save new filename index
	lda	$885c
	cmp	#$ff	;anything clicked before?
	beq	93$	;yes, so exit!
	sec
	sbc	$885b	;find old screen index
	tax
	lda	ClkCht,x	;calculate old screen index
	sta	R2L
	clc
	adc	#$0c
	sta	R2H
	jsr	InvertRectangle	;un-invert old choice
93$:	mvb	APITemp2,$885c	;save new index
94$:	rts


FBOXckch:	;This routine finds which filename is clicked on
	ldb	A6L,#$00	;set temporary index
92$:	ldx	A6L
	lda	ClkCht,x	;calculate filename box dimen.
	sta	R2L	;top
	clc
	adc	#$0c
	sta	R2H	;bottom
	ldw	R3,#$0047	;now set left
	ldw	R4,#$00c1	;  and right
	jsr	IsMseInRegion
	cmp	#$ff	;click was not on that filename
	bne	93$
	jsr	InvertRectangle	;if it was, invert the filename
	lda	A6L	;here is your new index?
	rts		;and exit
93$:	inc	A6L
	lda	A6L
	cbi	A6L,#$05	;otherwise go back and check
	bne	92$	;  the rest.
	lda	#$ff
	rts

ClkCht:	.byte	$24,$32,$40,$4e,$5c,$6a
	;chart for calculating the top and bottom scanlines

FBOXopen:	ldw	A6,#$8300	;Set start of filename grid
	ldx	$885c	;Find filename index
	bne	12$	;If first filename, your done!
	beq	13$
12$:	avw	#$11,A6	;Otherwise go to next filename
	dex
	bne	12$	;Keep on to fname index 
13$:	ldy 	#$00		;Now move filename 
14$:	lda	(A6),y	;   onto valid position
	sta	(A5),y
	iny
	cpy	#$11	;Up to max 17 characters
	bne	14$
	lda	#$00	;Then set EOL character as last
	sta	(A5),y
	rts


XUpload:	ldb	XMoreFlg,#$ff	; there is definitely more to get
	ldw	XBufWrPtr,#BACK_SCR_BASE
	ldw	XBufRdPtr,#BACK_SCR_BASE
	ldw	XBytesDone,#0
	ldb	XErrors,#0
	ldw	A2,#XFilename
	ldb	A3L,#$00
	jsr	OpenSF
	cpx	#0
	beq	10$
	ldw	R0,#XUpFlTxt
	jsr	OKBox
	jmp	keySetup
10$:	jsr	UseSystemFont
	jsr	XUpBox
	ldw	A2,#XUpFillBuf
	ldw	A3,#XUpRead
	ldw	A4,#XUpBadBlk
	jsr	XMOsend
	cpx	#0
	bne	15$
	jsr	ClrTrmScn	; clears the screen
	jsr	scnReset
	ldw	R0,#XUpDnTxt
	jsr	OKBox
	bra	20$
15$:	jsr	ClrTrmScn	; clears the screen
	jsr	scnReset	; will restore font
	ldw	R0,#XUpErTxt
	jsr	OKBox
20$:	jsr	keySetup
	rts

XUpDnTxt:	.byte	27,"Upload Complete!",27,0
XUpErTxt:	.byte	27,"Upload Cancelled.",27,0
XUpFlTxt:	.byte	27,"File failure. Upload cancelled",27,0

XUpFillBuf:	jsr	XUpdBox
	cwi	XBufRdPtr,#BACK_SCR_BASE + $1000
	bcs	20$
	cwi	XBufWrPtr,#BACK_SCR_BASE
	beq	50$	; one time uber-fill
10$:	avw	#128,XBytesDone
	ldx	#0
	rts
20$:	cpw	XBufRdPtr,XBufWrPtr
	beq	10$
	mvw	XBufRdPtr,A6
	ldw	A7,#BACK_SCR_BASE
30$:	ldy	#0
	lda	(A6),y
	sta	(A7),y
	inw	A6
	inw	A7
	cpw	A6,XBufWrPtr
	bcc	30$
	ldw	XBufRdPtr,#BACK_SCR_BASE
	svw	#$1000,XBufWrPtr
	lda	XMoreFlg
	beq	10$
50$:	ldb	XMoreFlg,#0
	mvw	XBufWrPtr,A2
	ldw	A3,#BACK_SCR_BASE + $1800
	jsr	GetSMem
	mvw	A2,XBufWrPtr
	cpx	#0
	bne	60$
	inc	XMoreFlg
60$:	ldx	#0
	rts


XUpRead:	cpw	XBufRdPtr,XBufWrPtr
	bne	20$
	lda	XMoreFlg
	bne	10$
	ldx	#$ff
	lda	#$00
	rts
10$:	jsr	XUpFillBuf
	jmp	XUpRead
20$:	ldy	#0
	mvw	XBufRdPtr,A6
	inw	XBufRdPtr
	ldx	#0
	lda	(A6),y
	rts	

XUpBadBlk:	inc	XErrors
	jsr	XUpdBox
	rts	

XferBox:	jsr	$c1a8	; i_GraphicsString
	.byte	$01,$5b,$00,$42	; MOVEPENTO Xl,Xh,Y
	.byte	$05,$01	; NEWPATTERN
	.byte	$03,$eb,$00,$79	; RETANGLETO  Xl,Xh,Y
	.byte	$01,$53,$00,$3c	; MOVEPENTO Xl,Xh,Y
	.byte	$05,$00	; NEWPATTERN
	.byte	$03,$e0,$00,$70	; RETANGLETO  Xl,Xh,Y
	.byte	$01,$50,$00,$38	; MOVEPENTO Xl,Xh,Y
	.byte	$07,$e1,$00,$71	; FRAME_RECTO  Xl,Xh,Y
	.byte	$06,$6b,$00,$65,$18,"Errors:",27,0
	jsr	$c1a8	; i_GraphicsString
	.byte	$06,$6b,$00,$56,$18,"Bytes:",27,0
	rts

XUpBox:	jsr	XferBox
	jsr	$c1a8	; i_GraphicsString
	.byte	$06,$79,$00,$44,$18,"Uploading...",27,0
	jsr	XUpdBox
	rts

XDnBox:	jsr	XferBox
	jsr	$c1a8	; i_GraphicsString
	.byte	$06,$79,$00,$44,$18,"Downloading...",27,0
	jsr	XUpdBox
	rts

XUpdBox:	lda	#0
	jsr	SetPattern
	ldw	R3,#$0086
	ldw	R4,#$00c0
	ldb	R2L,#$50
	ldb	R2H,#$68
	jsr	Rectangle
	ldw	R11,#$0086
	ldb	R1H,#$57
	mvw	XBytesDone,R0
	lda	#0
	jsr	PutDecimal
	ldw	R11,#$0086
	ldb	R1H,#$65
	mvb	XErrors,R0L
	ldb	R0H,#0
	lda	#0
	jsr	PutDecimal
	rts		; exit from screen routine


DoXDownload: 
	jsr	GotoFirstMenu
	jsr	SavColorScn
	jsr	ScreenSafe
	jsr	keyRestore
05$:	ldb	FBOXnod2,#$06	; reset disk icon
	cpb	curDrive,VPRGdriv
	bne	10$
	ldb	FBOXnod2,#0	; clear out disk icon
10$:	jsr	FBOXmvnm
	ldw	R0,#FBOXcbox	;now perform actual dialog box
	ldw	A6,#FBOXdnam
	jsr	DoDlgBox
	cbi	sysDBData,#$02	;was cancel hit?
	bne	15$
14$:	jmp	XferCncl	;back to start
15$:	cmp	#$06	;change disks
	bne	16$
	jsr	FBOXchgd
	bne	14$	;a change disk error?
	beq	05$
16$:	cmp	#$f0	;if other than filename, repeat
	beq	05$
	ldy	#0
	lda	(A5),y	;non-null filename
	beq	14$
	jsr	FBOXcrve
	cpx	#0
	bne	05$
	jsr	ClrTrmScn	; clears the screen
	jmp	XDownload

XDownload:	ldw	XBufWrPtr,#BACK_SCR_BASE
	ldw	XBufRdPtr,#BACK_SCR_BASE
	ldw	XBytesDone,#0
	ldb	XErrors,#0
	ldw	A2,#XFilename
	ldb	A3L,#PRG
	jsr	OpenSF
	cpx	#0
	beq	10$
	ldw	R0,#XDnFlTxt
	jsr	OKBox
	jmp	keySetup
10$:	jsr	UseSystemFont
	jsr	XDnBox
	ldw	A2,#XDnRcvByte
	ldw	A3,#XDnGooBlk
	ldw	A4,#XDnBadBlk
	jsr	XMOrcvCrc
	cpx	#0
	bne	15$
	ldb	A2L,#PRG	; ***TODO
	jsr	ClosSF
	jsr	ClrTrmScn	; clears the screen
	jsr	scnReset
	ldw	R0,#XDnDnTxt
	jsr	OKBox
	bra	20$
15$:	jsr	XDnFlush
	ldb	A2L,#PRG	; ***TODO
	jsr	ClosSF
	jsr	ClrTrmScn	; clears the screen
	jsr	scnReset	; will restore font
	ldw	R0,#XDnErTxt
	jsr	OKBox
20$:	jsr	keySetup
	rts

XDnDnTxt:	.byte	27,"Download Complete!",27,0
XDnErTxt:	.byte	27,"Download Cancelled.",27,0
XDnFlTxt:	.byte	27,"File failure. Download cancelled",27,0


XDnRcvByte:	pha
	mvw	XBufWrPtr,A6
	inw	XBufWrPtr
	ldy	#0
	pla
	sta	(A6),y
	rts	

XDnFlush:	ldw	A2,#BACK_SCR_BASE
	mvw	XBufWrPtr,A3
	jsr	PutSMem
	ldw	XBufWrPtr,#BACK_SCR_BASE
	rts

XDnGooBlk:	avw	#128,XBytesDone
	jsr	XUpdBox
	cwi	XBufWrPtr,#BACK_SCR_BASE+$1800
	bcc	10$
	jsr	XDnFlush
10$:	rts


XDnBadBlk:	inc	XErrors
	jsr	XUpdBox
	rts



FBOXswdv:	jsr	FBOXswd2
	cpx	#$00	;check for error!
	beq	21$
	jsr	FBOXswd2
	ldb	sysDBData,#$02	;error detected, return abort!
	jmp	RstrFrmDialog
21$:	ldb	sysDBData,#$f0	;denoted Drive icon
	jmp	RstrFrmDialog	;no error, jump back

FBOXswd2:	ldy	curDrive
	iny
	cpy	#11
	bne	11$
	ldy	#8
11$:	tya
	pha
	jsr	SetDevice
	pla
	cmp	curDrive
	beq	12$
	sta	curDrive
	bra	FBOXswd2
12$:	jsr	OpenDisk
	cpx	#0
	bne	FBOXswd2
	rts

FBOXchgd:
10$:	ldw	R0,#FBOXcdbx	;handle swap disk box
	jsr	DoDlgBox
	jsr	OpenDisk	;open new disk
	cpx	#0
	bne	10$
	rts

FBOXcdbx:
	.byte	$80 | $01
	.byte	11	;add text
	.byte	$0e,$12
	.word	FBOXtxt7
	.byte	$01,$0f,$42	;OK icon
	.byte	0	;end of defintion

FBOXmvnm:
	ldx	#A6	;discover the current drives
	jsr	GetPtrCurDkNm	;name. should this be a routine?
	ldy	#$00
17$:	lda	(A6),y	;copy disk name to buffer...
	cmp	#$a0	;so that a 0 can be added
	beq	18$	;and thus be readable by
	sta	FBOXdnam,y	;the dialog box routines
	iny		
	cmp	#$a0	
	beq	18$
	cpy	#18
	bne	17$
18$:	lda	#0	;now add rol to end
	sta	FBOXdnam,y
	rts


FBOXcrve:	jsr	GetDirHead	;get BAM into memory
	cpx	#0
	bne	99$	; exit on disk error
	ldw	R5,#$8200	;point to BAM
	jsr	CalcBlksFree	
	lda	R4H
	bne	11$
	lda	R4L
	cmp	#$14	;20 blocks minimum
	bcs	11$
	ldw	A6,#FBOXtxt9	;not enough blocks
10$:	ldw	R0,#FBOXnbox
	jsr	DoDlgBox	;whine and exit
	ldx	#$ff
99$:	rts
11$:	ldb	$886e,#$ff	;check for file existance
	mvw	A5,R6
	jsr	FindFile
	cpx	#$05	;this or that error?
	beq	13$
	cpx	#$00
	bne	12$
	ldw	A6,#FBOXtxt8
	bra	10$
12$:	ldw	A6,#FBOXtxtA	;other errors
	bra	10$
13$:	ldy	#0
14$:	lda	(A5),y
	sta	XFilename,y
	beq	15$
	iny
	bne	14$
15$:	ldx	#$00
	rts		;enough blocks

FBOXnbox:
	.byte	$80 | $01
	.byte	12	;add text
	.byte	$20,$1b
	.byte	A6
	.byte	$02,$0f,$40	;cancel icon
	.byte	0	;end of defintion

FBOXcbox:	.byte	$80 | $01
	.byte	11	;add text
	.byte	$16,$11
	.word	FBOXtxt4
	.byte	12
	.byte	$43,$11
	.byte	A6
	.byte	11	;add text
	.byte	$16,$22
	.word	FBOXtxt5
	.byte	13	;GetString
	.byte	$16,$33	;XOffset.Yalso
	.byte	A5	
	.byte	18	;maximum # of chars to accept.
	.byte	$02,$10,$47	;Cancel icon
FBOXnod1:	.byte	18	;drive icon
	.byte	$09,$47
	.word	FBOXdcn2
FBOXnod2:	.byte	$06,$02,$47	;DISK icon
	.byte	0	;end of defintion
FBOXdcn2:
	.word	FBOXdmag	;graphic location
	.word	0	;leave this at 0
	.byte	$06,$10	;width, set below
	.word	FBOXswdv	;location of action routine


FBOXtxt0:	.byte	$18,"Please select an option:",27,0
FBOXtxt1:	.byte	$18,"new work file",27,0
FBOXtxt2:	.byte	$18,"existing file",27,0
FBOXtxt3:	.byte	$18,"to deskTop",27,0
FBOXtxt4:  	.byte	$18,"On Disk:",27,0
FBOXtxt5:	.byte	$18,"Please enter new filename:",27,0
FBOXtxt6:  	.byte	$18,"Enter new file name:",27,0		
FBOXtxt7:  	.byte	$18,"Insert new disk into disk drive.",27,0
FBOXtxt8:  	.byte	$18,"ERROR: File exists!",27,0
FBOXtxt9:  	.byte	$18,"ERROR: Disk full!",27,0
FBOXtxtA:  	.byte	$18,"ERROR: Cannot create!",27,0


.ramsect
XFilename:	.block 20
FBOXdnam:	.block 20
XBufWrPtr:	.block 2
XBufRdPtr:	.block 1
XMoreFlg:	.block 1
XBytesDone:	.block 2
XErrors:	.block 1
</pre></body></html>